(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED " 9-Aug-2022 20:44:48" {DSK}<home>larry>medley>test>new>printing>HCFILES.;3 4231   

      :CHANGES-TO (FNS HCFILES)

      :PREVIOUS-DATE " 7-Aug-2022 22:40:13" {DSK}<home>larry>medley>test>new>printing>HCFILES.;2)


(PRETTYCOMPRINT HCFILESCOMS)

(RPAQQ HCFILESCOMS ((FNS BADFILE HCFILES)
                    (INITVARS (HCFILES)
                           (BADFILES))))
(DEFINEQ

(BADFILE
  [LAMBDA NIL                                              (* ; "Edited 22-Jun-2022 09:40 by larry")
    (pushnew BADFILES TFILE)
    (LET [(STR (OPENSTREAM "BADFILES.TXT" 'APPEND]
         (SETFILEPTR STR -1)
         (PRINT TFILE STR)
         (CLOSEF STR))
    (RETFROM 'HCFILES])

(HCFILES
  [LAMBDA (TFILE DEST REDOFLG TOPDIRLEN)
    (DECLARE (SPECVARS TFILE))                               (* ; "Edited  9-Aug-2022 20:44 by lmm")
    (if (NULL TFILE)
        then (SETQ TFILE MEDLEYDIR))
    (COND
       ((DIRECTORYNAMEP TFILE)

        (* ;; "canonicalize")

        (SETQ TFILE (DIRECTORYNAME TFILE))
        [OR TOPDIRLEN (SETQ TOPDIRLEN (CL:LENGTH (FILENAMEFIELD.STRING TFILE 'DIRECTORY]
        (CL:UNLESS DEST
            (ShellCommand (CONCAT "mkdir -p " (UNIX-GETENV "MEDLEYDIR")
                                 "/tmp/psfiles/"))
            (SETQ DEST (MEDLEYDIR "tmp/psfiles" NIL T T)))

        (* ;; "first deal with files in this directory")

        (for EXT in '("TED*" "SKETCH") do (for X in (DIRECTORY (CONCAT TFILE "*." EXT ";*"))
                                             do (HCFILES X DEST REDOFLG TOPDIRLEN)))

        (* ;; " then deal with subdirs ")

        (for X in (DIRECTORY (CONCAT TFILE "*")) when [for SKIP in '(">." ">dinfo>")
                                                         always (NOT (STRPOS SKIP (L-CASE X]
           when (DIRECTORYNAMEP X) do (HCFILES X DEST REDOFLG TOPDIRLEN)))
       [(SETQ TFILE (INFILEP TFILE))
        (LET* ((TF (UNPACKFILENAME.STRING TFILE))
               (NAME (LISTGET TF 'NAME))
               (DIR (LISTGET TF 'DIRECTORY))
               (PSFILE (PACKFILENAME.STRING
                        'EXTENSION
                        (if (EQ REDOFLG 'IP)
                            then "IP"
                          else "PS")
                        'NAME
                        (if (EQ DEST T)
                            then                             (* ; "with the tedit file")
                                 NAME
                          else (CONCAT [PACK (SUBST '- '> (UNPACK (SUBSTRING DIR (IPLUS 2 TOPDIRLEN)
                                                                         -1]
                                      "-" NAME))
                        'HOST
                        (LISTGET TF 'HOST)
                        'DIRECTORY
                        (if (EQ DEST T)
                            then DIR
                          else DEST)))
               (TEXTSTREAM))
              (if (AND (NOT REDOFLG)
                       (INFILEP PSFILE))
                  then                                       (* ; " do nothing")
                       (PRINTOUT T PSFILE " already there" T)
                elseif (EQ REDOFLG 'TEST)
                  then (PRINTOUT T TFILE "-> " PSFILE T)
                       (CLOSEF (OPENTEXTSTREAM TFILE))
                elseif (MEMBER TFILE BADFILES)
                  then (PRINTOUT T "Skipping " TFILE " on BADFILES")
                else (PRINTOUT T "Converting " TFILE " to " PSFILE "...")
                     (TEDIT.FORMAT.HARDCOPY (SETQ TEXTSTREAM (OPENTEXTSTREAM TFILE))
                            PSFILE T NIL NIL NIL (if (EQ REDOFLG 'IP)
                                                     then 'INTERPRESS
                                                   else 'POSTSCRIPT))
                     (printout T " DONE" T)
                     (CLOSEF? TEXTSTREAM]
       (T (PRINTOUT T "no such file " T])
)

(RPAQ? HCFILES )

(RPAQ? BADFILES )
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (467 4163 (BADFILE 477 . 785) (HCFILES 787 . 4161)))))
STOP
